<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009"
        xmlns:s="library://ns.adobe.com/flex/spark"
        xmlns:esri="http://www.esri.com/2008/ags"
        xmlns:components="components.*"
        title="A tiled map service">

    <fx:Script>
        <![CDATA[
            import com.esri.ags.events.MapEvent;
            import com.esri.ags.geometry.MapPoint;
            import com.esri.ags.geometry.WebMercatorMapPoint;

            import flash.sensors.Geolocation;

            private var geo:Geolocation;

            private function myMap_loadHandler(event:MapEvent):void
            {
                if (Geolocation.isSupported)
                {
                    geo = new Geolocation();
                    zoomToCurrentLocationBtn.visible = !geo.muted;
                    geo.addEventListener(StatusEvent.STATUS, geo_statusHandler, false, 0, true);
                }
            }

            private function geo_statusHandler(event:StatusEvent):void
            {
                zoomToCurrentLocationBtn.visible = !geo.muted;
                if (geo.muted)
                {
                    myLocationGraphic.visible = false;
                }
            }

            private function zoomToCurrentLocation_clickHandler(event:MouseEvent):void
            {
                busyIndicator.visible = true;
                zoomToCurrentLocationBtn.enabled = false;
                geo.addEventListener(GeolocationEvent.UPDATE, geo_updateHandler);
            }

            private function geo_updateHandler(event:GeolocationEvent):void
            {
                busyIndicator.visible = false;
                zoomToCurrentLocationBtn.enabled = true;
                geo.removeEventListener(GeolocationEvent.UPDATE, geo_updateHandler);

                if (isNaN(event.longitude) || isNaN(event.longitude))
                {
                    // values are NaN in Airplane mode
                    myLocationGraphic.visible = false;
                }
                else
                {
                    // convert the longitude and latitude values to web mercator as the basemap is in web mercator
                    var currentLocation:MapPoint = new WebMercatorMapPoint(event.longitude, event.latitude);

                    // show and zoom to current location
                    myLocationGraphic.geometry = currentLocation;
                    myLocationGraphic.visible = true;
                    myMap.centerAt(currentLocation);
                    myMap.level = 16;
                }
            }
        ]]>
    </fx:Script>

    <esri:Map id="myMap"
              attributionVisible="false"
              level="2"
              load="myMap_loadHandler(event)"
              wrapAround180="true"
              zoomSliderVisible="false">
        <esri:extent>
            <esri:Extent xmin="-19325128" ymin="-10948057" xmax="-539964" ymax="18364625">
                <esri:SpatialReference wkid="102100"/>
            </esri:Extent>
        </esri:extent>
        <esri:ArcGISTiledMapServiceLayer url="http://server.arcgisonline.com/ArcGIS/rest/services/World_Street_Map/MapServer"/>
        <esri:GraphicsLayer>
            <esri:Graphic id="myLocationGraphic" visible="false">
                <esri:symbol>
                    <esri:PictureMarkerSymbol/>
                </esri:symbol>
            </esri:Graphic>
        </esri:GraphicsLayer>
    </esri:Map>

    <s:Button id="zoomToCurrentLocationBtn"
              bottom="6"
              click="zoomToCurrentLocation_clickHandler(event)"
              horizontalCenter="0"
              skinClass="skins.ImageButtonSkin"
              visible="false"/>

    <s:BusyIndicator id="busyIndicator"
                     horizontalCenter="0"
                     verticalCenter="0"
                     visible="false"/>

    <components:Compass right="20" top="20"
                        click="myMap.mapRotation = 0"
                        mapRotation="{myMap.mapRotation}"
                        skinClass="skins.CompassSkin"/>

</s:View>
